" echo "My best vim == >^.^< =="

" Quick Start
" -----------
" 1) clone Vundle for vim plugin magager
" >>>>> git clone https://github.com/VundleVim/Vundle.vim.git ~/.vim/bundle/Vundle.vim
"
" 2) add your vim config here and copy it
" >>>>> cp .vimrc ~/
"
" 3) open your vim and run command for plugin install
" >>>>> :PluginInstall



" ==========vim plugin manager========== {{{
" ==================ä½¿ç”¨Vundleç®¡ç†æ’ä»¶========================
set nocompatible        " be iMproved, required
filetype off            " required

" polyglotæ’ä»¶è¯­æ³•åŒ…å†²çªè§£å†³æ–¹æ¡ˆ
" let g:polyglot_disabled = ['markdown']          "ç¦ç”¨ä¸ªäººæ’ä»¶è¯­æ³•
" let g:polyglot_disabled = ['markdown.plugin']   "ç¦ç”¨polyglotä¸­çš„è¯­æ³•
" let g:polyglot_disabled = ['plantuml.plugin']   "ç¦ç”¨polyglotä¸­çš„è¯­æ³•
" let g:polyglot_disabled = ['sensible']          "å…³é—­polyglotçš„defaultè®¾ç½®
" let g:polyglot_disabled = ['ftdetect']          "ç¦ç”¨polyglotä¸­çš„æ–‡ä»¶æ¢æµ‹

" set the runtime path to include Vundle and initialize
set rtp+=~/.vim/bundle/Vundle.vim
call vundle#begin()
" alternatively, pass a path where Vundle should install plugins
" call vundle#begin('$VIM/vimfiles/bundle')
" let Vundle manage Vundle, required
Plugin 'VundleVim/Vundle.vim'               "æ’ä»¶ç®¡ç†
Plugin 'tpope/vim-surround'                 "å¯ä½¿å—surroud, å¦‚ç»™word -> {word}
Plugin 'tpope/vim-unimpaired'               "ç¼“å†²åŒºã€å‚æ•°ã€quickfixã€ä½ç½®ã€æ ‡ç­¾åˆ—è¡¨çš„éå†å¿«æ·é”®
Plugin 'tpope/vim-commentary'               "ä»£ç æ³¨é‡Šgc
Plugin 'tpope/vim-bundler'                  "gfè·³è½¬pathçš„è®¾ç½®ç›¸å…³, ä»¥åŠtags, å¥½åƒæ²¡å•¥æ•ˆæœï¼Œå¾…ç ”ç©¶
Plugin 'iamcco/mathjax-support-for-mkdp'    "markdownæ•°å­¦å…¬å¼
Plugin 'iamcco/markdown-preview.nvim'       "markdowné¢„è§ˆ
Plugin 'aklt/plantuml-syntax'               "plantumlè¯­æ³•é«˜äº®
Plugin 'weirongxu/plantuml-previewer.vim'   "plantumlé¢„è§ˆ, need sudo apt-get install graphviz
Plugin 'tyru/open-browser.vim'              "æµè§ˆå™¨é¢„è§ˆæ¸²æŸ“å›¾, plantumlç”¨
Plugin 'octol/vim-cpp-enhanced-highlight'   "cppè¯­æ³•é«˜äº®
Plugin 'voldikss/vim-translator'            "vimç¿»è¯‘å·¥å…·
Plugin 'tikhomirov/vim-glsl'                "openglç€è‰²å™¨è¯­è¨€è¯­æ³•é«˜äº®
" Plugin 'scrooloose/vim-slumlord'            "plantumlé¢„è§ˆ, ä½¿ç”¨ASCIIç åœ¨vimå†…éƒ¨é¢„è§ˆï¼Œæ•ˆæœå¾ˆå·®
" Plugin 'sheerun/vim-polyglot'               "è¯­æ³•åŒ…ï¼ŒåŒ…å«å¤§é‡è¯­æ³•å¦‚c++ã€cmakeã€glslã€markdownã€plantuml... å¤ªè¿‡é‡é‡çº§ï¼Œå†…éƒ¨ä¼šä¿®æ”¹å¾ˆå¤šé…ç½®
" All of your Plugins must be added before the following line
call vundle#end()            " required

filetype plugin indent on    " required
" To ignore plugin indent changes, instead use:
" filetype plugin on
"
" Brief help
" :PluginList       - lists configured plugins
" :PluginInstall    - installs plugins; append `!` to update or just :PluginUpdate
" :PluginSearch foo - searches for foo; append `!` to refresh local cache
" :PluginClean      - confirms removal of unused plugins; append `!` to auto-approve removal
"
" see :h vundle for more details or wiki for FAQ
" Put your non-Plugin stuff after this line
" ==================ä½¿ç”¨Vundleç®¡ç†æ’ä»¶========================
" }}}

" ==========global variables========== {{{
" translator variable
" let g:translator_proxy_url = 'socks5://127.0.0.1:1080'
let g:translator_default_engines = ['bing', 'haici']

let g:quickfix_l_is_open = 0

let g:nrformats_origin = ''

let g:hex_show = 0

let g:v_beg = 0
let g:v_mid = 0
let g:v_end = 0
let g:v_last_p = 0

let g:h_beg = 0
let g:h_mid = 0
let g:h_end = 0
let g:h_last_p = 0
" }}}

" ==========normal settings========== {{{
" è®©é…ç½®å˜æ›´ç«‹å³ç”Ÿæ•ˆ, ä½¿ç”¨å¿«æ·é”®è¿è¡Œsourceæ„Ÿè§‰æ›´å¥½
" autocmd BufWritePost $MYVIMRC source $MYVIMRC

" å¯åŠ¨æ—¶è‡ªåŠ¨åŠ è½½matchitæ’ä»¶ï¼Œç”¨äº%é…å¯¹æ ‡ç­¾é—´è·³è½¬, å¦‚htmlæ ‡ç­¾ï¼Œä½†æ˜¯(),{}ç­‰æ˜¯é»˜è®¤æ”¯æŒçš„
runtime macros/matchit.vim

" colorscheme morning   " è®¾ç½®é…è‰²æ–¹æ¡ˆ

filetype indent on      " è‡ªé€‚åº”ä¸åŒè¯­è¨€çš„æ™ºèƒ½ç¼©è¿›
filetype on             " å¼€å¯æ–‡ä»¶æ£€æµ‹
syntax on               " å¼€å¯è¯­æ³•é«˜äº®

set pastetoggle=<F5>    " pasteä¸paste!çš„åˆ‡æ¢ï¼Œå¯ç”¨pasteåœ¨ä½¿ç”¨ç³»ç»Ÿç²˜è´´å‘½ä»¤æ—¶å¯ä»¥é˜²æ­¢å¥‡æ€ªçš„ç¼©è¿›

" æŠŠæ‰€æœ‰æ•°å­—å½“æˆåè¿›åˆ¶ï¼Œä¸ç®¡æ˜¯å¦ä»¥0å¼€å¤´æˆ–æ˜¯0xå¼€å¤´
let g:nrformats_origin = &nrformats
set nrformats=

" tab å‘½ä»¤è¡¥å…¨æ¨¡å¼, æ­¤æ¨¡å¼tabä¼šæ˜¾ç¤ºè¡¥å…¨åˆ—è¡¨
set wildmenu
set wildmode=full

set ignorecase          "å¼€å¯å¤§å°å†™ä¸æ•æ„Ÿï¼Œä½œç”¨äºæŸ¥æ‰¾å’Œè¡¥å…¨
set smartcase           "å¼€å¯æ™ºèƒ½å¤§å°å†™æ•æ„Ÿï¼Œè¾“å…¥å¤§å†™åˆ™åŒ¹é…å¤§å†™ï¼Œå¦åˆ™ä½¿ç”¨ignorecase

set history=200         "è®¾ç½®å‘½ä»¤å›æœ”å†å²ä¸º200æ¡ï¼Œé»˜è®¤ä¸º50

set number              " æ˜¾ç¤ºè¡Œå·
set incsearch           " æŸ¥æ‰¾æ˜¯é¢„è§ˆç¬¬ä¸€å¤„åŒ¹é…
set hlsearch            " é«˜äº®æŸ¥æ‰¾åŒ¹é…é¡¹
" :nohlearch å‘½ä»¤æš‚æ—¶å…³é—­é«˜äº®æ¨¡å¼ï¼Œç›´åˆ°æ‰§è¡Œæ–°çš„æˆ–é‡å¤çš„æŸ¥æ‰¾å‘½ä»¤
" <C-l>åŸæ˜¯æ¸…å±é‡ç»˜å¿«æ·é”®ï¼Œå°†å…¶å’Œnohlearchä¸€èµ·ä½œç”¨
" nnoremap <silent> <C-l> :<C-u>nohlsearch<CR><C-l>
" ä½†æ˜¯æ¸…å±ä¼šä½¿å±å¹•é—ªçƒä¸€ä¸‹ï¼Œæ•ˆæœéå¸¸å·®ï¼Œæ‰€ä»¥é‡æ˜ å°„<C-p>ä¸ºæš‚æ—¶å…³é—­é«˜äº®æ¨¡å¼
" å¹¶é‡æ˜ å°„<leader><C-l>ä¸ºæ¸…å±ï¼Œå› ä¸ºæˆ‘ä»¬æƒ³å°†<C-l>ç”¨äºå…¶ä»–åŠŸèƒ½
nnoremap <silent> <C-p> :<C-u>nohlsearch<CR>
nnoremap <silent> <leader><C-l> :<C-u>nohlsearch<CR><C-l>

" å®šä¹‰å¿«æ·é”®å‰ç¼€<Leader>, todo: æœ‰å†²çª
" let mapleader=";"
" è®¾ç½®å¿«æ·é”®å°†é€‰ä¸­æ–‡æœ¬å¤åˆ¶åˆ°ç³»ç»Ÿå‰ªåˆ‡æ¿
vnoremap <Leader>y "+y
" è®¾ç½®å¿«æ·é”®å°†ç³»ç»Ÿå‰ªåˆ‡æ¿å†…å®¹ç²˜è´´è‡³vim
nnoremap <Leader>p "+p
" è®¾ç½®å¿«æ·é”®æ‰“å¼€vimrc
nnoremap <Leader>ev :vsplit $MYVIMRC<CR>:<c-u>resize 9999<CR>:<c-u>vertical resize 9999<CR>:echo "edit vimrc ..."<CR>
" è®¾ç½®å¿«æ·é”®åº”ç”¨vimrc
nnoremap <Leader>sv :source $MYVIMRC<CR>:<C-u>nohlsearch<CR>:echo "run source vimrc ok!"<CR>

" æ’å…¥æ¨¡å¼ä¸‹è½¬æ¢å…‰æ ‡å‰å•è¯/å­—ç¬¦ä¸²å¤§å°å†™
" inoremap <C-y> <esc>m0bviw~`0a
" inoremap <C-f> <esc>m0BviW~`0a
" æ”¹è¿›ç‰ˆæœ¬ï¼Œè€ƒè™‘å‘½åç©ºé—´god::VIM,
" ç°åœ¨æƒ³åœ¨ä¸­é—´åŠ ä¸€ä¸ªå‘½åç©ºé—´å˜æˆgod::EDITOR::VIM
" äºæ˜¯ç¼–è¾‘æˆè¿™æ ·god::editorVIM,
" å…‰æ ‡åœ¨editorçš„æœ«å°¾ï¼Œ<C-y>å°†å…¶è½¬æ¢ä¸ºå¤§å†™ï¼Œå¯æ˜¯VIMä¹Ÿè¢«è½¬æ¢äº†
" ä½†æ˜¯è¿™åˆå¼•å…¥äº†å¦ä¸€ä¸ªbug, å½“å…‰æ ‡ä½äºå•è¯é¦–å­—æ¯æ—¶ï¼Œåªæœ‰å…‰æ ‡ä¸‹çš„å­—æ¯ä¼šè¢«è½¬æ¢
inoremap <C-y> <esc>mzvgew~`za
inoremap <C-f> <esc>mzvgEW~`za

" multi map for esc, But i dont iwe it
" inoremap jk <esc>

" è®¾ç½®å¿«æ·é”®å½“å‰ç¼“å†²åŒºå…³é”®å­—è¡¥å…¨ï¼Œå½“æ™®é€šå…³é”®å­—å¤ªå¤šæ—¶ä½¿ç”¨ï¼Œ
" åˆå› ä¸ºå½“å‰ç¼“å†²åŒºå…³é”®å­—è¡¥å…¨é»˜è®¤<C-x><C-n>å¤ªéº»çƒ¦ï¼Œæ‰€ä»¥è¿›è¡Œè‡ªå®šä¹‰
" ä¸‹é¢åŒç†, todo: <C-m>æœ‰å†²çª
" inoremap <C-m><C-n> <C-x><C-n> "å½“å‰ç¼“å†²åŒºå…³é”®å­—
" inoremap <C-m><C-i> <C-x><C-i> "includeæ–‡ä»¶å…³é”®å­—
" inoremap <C-m><C-]> <C-x><C-]> "æ ‡ç­¾æ–‡ä»¶å…³é”®å­—
" inoremap <C-m><C-k> <C-x><C-k> "å­—å…¸æŸ¥æ‰¾
" inoremap <C-m><C-l> <C-x><C-l> "æ•´è¡Œè¡¥å…¨
" inoremap <C-m><C-f> <C-x><C-f> "æ–‡ä»¶åè¡¥å…¨
" inoremap <C-m><C-o> <C-x><C-o> "æ™ºèƒ½è¡¥å…¨

" é‡æ˜ å°„<C-d>ä¸ºå…‰æ ‡æŒ‡å‘å±å¹•æœ€åä¸€è¡Œç„¶åæ‰§è¡Œzz, å³å°†å±å¹•æœ€åä¸€è¡Œæ˜¾ç¤ºåœ¨å±å¹•ä¸­é—´,ä¹Ÿå¯ç†è§£ä¸ºå‘ä¸‹ç¿»åŠ¨åŠé¡µ
nnoremap <C-d> Lzz
" é‡æ˜ å°„<C-u>ä¸ºå…‰æ ‡æŒ‡å‘å±å¹•æœ€åä¸€è¡Œç„¶åæ‰§è¡Œzz, å³å°†å±å¹•æœ€åä¸€è¡Œæ˜¾ç¤ºåœ¨å±å¹•ä¸­é—´,ä¹Ÿå¯ç†è§£ä¸ºå‘ä¸Šç¿»åŠ¨åŠé¡µ
nnoremap <C-u> Hzz
" æ˜ å°„<C-j>ä¸ºå…‰æ ‡æŒ‡å‘å±å¹•æœ€åä¸€è¡Œç„¶åæ‰§è¡Œzz, å³å°†å±å¹•æœ€åä¸€è¡Œæ˜¾ç¤ºåœ¨å±å¹•ä¸­é—´, ä¹Ÿå¯ç†è§£ä¸ºå‘ä¸‹ç¿»åŠ¨åŠé¡µ
" nnoremap <C-j> Lzz
" æ˜ å°„<C-k>ä¸ºå…‰æ ‡æŒ‡å‘å±å¹•æœ€åä¸€è¡Œç„¶åæ‰§è¡Œzz, å³å°†å±å¹•æœ€åä¸€è¡Œæ˜¾ç¤ºåœ¨å±å¹•ä¸­é—´, ä¹Ÿå¯ç†è§£ä¸ºå‘ä¸Šç¿»åŠ¨åŠé¡µ
" nnoremap <C-k> Hzz

" %% æ˜ å°„ä¸º %:h, å³å½“å‰æ´»åŠ¨ç¼“å†²åŒºæ–‡ä»¶è·¯å¾„ï¼ˆå»æ‰æ–‡ä»¶åï¼‰
cnoremap <expr> %% getcmdtype() == ':' ? expand('%:h').'/' : '%%'

" f7 æ‰§è¡Œ!ctags -R
nnoremap <F7> :!ctags -R<CR>
" æ¯æ¬¡ä¿å­˜æ–‡ä»¶æ—¶è‡ªåŠ¨è°ƒç”¨ctags -R, è¿™ç§æ–¹å¼ä¸å¥½ï¼Œå®ƒä¼šä½¿å¾—ä¿å­˜å˜æ…¢ï¼Œå¹¶ä¸”ä¸éœ€è¦tagsçš„é¡¹ç›®åœ¨ä¿å­˜æ—¶ä¹Ÿä¼šç”Ÿæˆ
" autocmd BufWritePost * call system("ctags -R")

" &æ˜ å°„ä¸º&&, å³é‡å¤ä¸Šæ¬¡æ›¿æ¢å‘½ä»¤å¸¦flag
nnoremap & :&&<CR>
xnoremap & :&&<CR>

" spaceè¿›å…¥å¯è§†æ¨¡å¼å¹¶é€‰ä¸­å•è¯
nnoremap <space> viw

" map Y to yank text from cursor to the end of line
nnoremap Y y$
" }}}

" ==========fold setting========== {{{
" no fold when open file
set foldlevelstart=99

" vim fold setting
augroup filetype_vim
    autocmd!
    autocmd FileType vim setlocal foldmethod=marker
augroup END

" cpp fold setting
augroup filetype_cpp
    autocmd!
    autocmd FileType cpp setlocal foldmethod=syntax
    autocmd FileType c setlocal foldmethod=syntax
    " ä½ç‰ˆæœ¬çš„vimå¯èƒ½é€ æˆæ— æ³•å¿å—çš„å¡é¡¿!!! è¯·æ›¿æ¢ä¸ºä¸‹é¢çš„æ–¹å¼ï¼Œä½†æŠ˜å åŠŸèƒ½ä¼šå¤§æ‰“æŠ˜æ‰£
    " autocmd FileType cpp setlocal foldmethod=indent
    " autocmd FileType c setlocal foldmethod=indent
augroup END

" toggle foldcolumn
nnoremap <leader>d :call <SID>FoldColumnToggle()<CR>
function! s:FoldColumnToggle()
    if &foldcolumn
        setlocal foldcolumn=0
    else
        setlocal foldcolumn=4
    endif
endfunction
" }}}

" ==========tab setting========== {{{
set tabstop=4           " tab/åˆ¶è¡¨ç¬¦ å››ä¸ªç©ºæ ¼
set shiftwidth=4        " >> << å››ä¸ªç©ºæ ¼
set expandtab           " tab/åˆ¶è¡¨ç¬¦ å±•å¼€ä¸ºå››ä¸ªç©ºæ ¼
set softtabstop=4       " è¿ç»­4ä¸ªç©ºæ ¼è§†ä¸ºä¸€ä¸ªtab/åˆ¶è¡¨ç¬¦
" retabå‘½ä»¤å¯ä»¥æŒ‰ç…§ä¸Šé¢è®¾ç½®çš„è§„åˆ™æ ¼å¼åŒ–ä»£ç 
" }}}

" ==========file encode setting========== {{{
set fileformat=unix             "unix æ–‡ä»¶æ ¼å¼ï¼Œ\nä¸ºè¡Œç»“æŸç¬¦ï¼Œå®é™…æµ‹è¯•æ— æ•ˆï¼Œåˆ›å»ºæ–‡ä»¶æ—¶ä¸ç³»ç»Ÿä¸€è‡´!!! å¯ä»¥æ‰“å¼€æ–‡ä»¶åæ‰‹åŠ¨è®¾ç½®ç”Ÿæ•ˆ
set fileformats=unix,dos,mac    "è®¾ç½®vimæ”¯æŒçš„ç³»ç»Ÿæ–‡ä»¶æ ¼å¼
set nobomb                      "utf-8æ ‡å‡†æ ¼å¼ï¼Œbombå¾®è½¯ç”¨çš„å¤š
set encoding=utf-8              "vim å†…éƒ¨ä½¿ç”¨çš„å­—ç¬¦ç¼–ç æ–¹å¼ï¼ŒåŒ…æ‹¬ç¼“å†²åŒºã€èœå•æ–‡æœ¬ã€æ¶ˆæ¯æ–‡æœ¬ç­‰
set fileencoding=utf-8          "vim å½“å‰ç¼–è¾‘çš„æ–‡ä»¶å­—ç¬¦ç¼–ç æ–¹å¼ï¼Œä¿å­˜å’Œæ–°å»ºéƒ½æ˜¯è¿™ç§ç¼–ç æ ¼å¼
" vimå¯åŠ¨æ—¶æŒ‰ç…§åˆ—è¡¨è¿›è¡Œæ¢æµ‹ï¼Œå¹¶å°†fileencodingè®¾ç½®ä¸ºæ­¤ç¼–ç æ–¹å¼ï¼Œ
" è¿™å¾ˆå¥½ç†è§£ï¼Œå³æ‰“å¼€æ–‡ä»¶å’Œä¿å­˜æ–‡ä»¶é»˜è®¤æƒ…å†µä¸‹åº”è¯¥ä¿æŒç¼–ç æ–¹å¼ä¸å˜
" å¯ä»¥ç†è§£ä¸ºè®¾ç½®vimæ”¯æŒçš„æ–‡ä»¶ç¼–ç æ ¼å¼ï¼Œç±»ä¼¼fileformats
set fileencodings=utf-8,gbk,ucs-bom,default,latin1
" termencoding: vimå·¥ä½œç»ˆç«¯çš„ç¼–ç æ–¹å¼
" }}}

" ==========statusline setting========== {{{
" set statusline
" set statusline=%f   "path to the file
" set statusline+=%=  "switch to the right side
" set statusline+=%l  "current line number
" set statusline+=/   "separator
" set statusline+=%L  "total line number
" set statusline+=%y  "file type
" }}}

" ==========abbreviations setting========== {{{
" ç¼©ç•¥å¥abbreviationsç±»ä¼¼map, æ’å…¥æ¨¡å¼ä¸‹è¾“å…¥x@å¯ä»¥å¿«é€Ÿæ›¿æ¢ä¸ºå¯¹åº”é‚®ç®±
" ä¸ç”¨æ‹…å¿ƒæ‰€æœ‰x@éƒ½è¢«æ›¿æ¢ï¼Œå› ä¸ºæœ‰iskeywordä¿æŠ¤
iabbrev @e wonderful27x@126.com
iabbrev @g wonderful27x@gmail.com
iabbrev @w wonderful27x@outlook.com
iabbrev @y wangdef@xxxxxxx.com
" }}}

" ==========forbiden key setting========== {{{
" æ’å…¥æ¨¡å¼å’Œå‘½ä»¤è¡Œæ¨¡å¼ç¦ç”¨é€€æ ¼åˆ é™¤é”®ï¼Œé˜²æ­¢ä¸è‰¯ä¹ æƒ¯
inoremap <backspace> <nop>
cnoremap <backspace> <nop>
" }}}

" ==========more operator-pending mappings for markdown========== {{{
" more operator-pending mappings
" for markdown "change inside/around heading"
" for example in markdown:
" Topic one
" =========
" run normal command: cih
" Topic one will be deleted and change to insert mode
" what does the map do? seperate as heres:
" ?^==\+$
" :nohlsearch
" kvg_
augroup markdown_group
    autocmd!
    autocmd FileType markdown onoremap <buffer> ih :<c-u>execute "normal! ?\\(^==\\+$\\\|^--\\+$\\)\r:nohlsearch\rkvg_"<CR>
    autocmd FileType markdown onoremap <buffer> ah :<c-u>execute "normal! ?\\(^==\\+$\\\|^--\\+$\\)\r:nohlsearch\rg_vk0"<CR>
augroup END
" }}}

" ==========visual search for */# setting========== {{{
" å¯è§†æ¨¡å¼æŒ‰*/#å¯¹é€‰ä¸­æ–‡æœ¬è¿›è¡ŒæŸ¥è¯¢
" æ³¨æ„ç¬¦å·:, è¿™æ˜¯ä¸€æ¡å‘½ä»¤è¡Œæ˜ å°„ï¼Œè€Œå‘½ä»¤è¡Œæˆ–æ’å…¥æ¨¡å¼ä¸‹<C-u>ä»£è¡¨æ¸…é™¤è‡³è¡Œé¦–
xnoremap * :<C-u>call <SID>VSetSearch('/')<CR>/<C-R>=@/<CR><CR>
xnoremap # :<C-u>call <SID>VSetSearch('?')<CR>?<C-R>=@/<CR><CR>
    function! s:VSetSearch(cmdtype)
    let temp = @s
    norm! gv"sy
    let @/ = '\V' . substitute(escape(@s, a:cmdtype.'\'), '\n', '\\n', 'g')
    let @s = temp
endfunction
" }}}

" ==========convenient map for grep searching========== {{{
" traverse quickfix window history
nnoremap <leader>q :colder<CR>
nnoremap <leader>Q :cnewer<CR>
" better use than <leader>Q
nnoremap <leader><leader>q :cnewer<CR>

" command completement of search
nnoremap <leader>sw /<C-r><C-w>
nnoremap <leader><leader>sw /\<<C-r><C-w>\><Left><Left>

" command completement of grep
nnoremap <leader>sa :vimgrep // ./**/*.cpp ./**/*.cc ./**/*.c ./**/*.h ./**/*.hpp<C-f>:call cursor(0,11)<CR>
nnoremap <leader>sc :vimgrep // ./**/*.cpp ./**/*.cc ./**/*.c<C-f>:call cursor(0,11)<CR>
nnoremap <leader>sh :vimgrep // ./**/*.h ./**/*.hpp<C-f>:call cursor(0,11)<CR>
nnoremap <leader>s% :vimgrep // %<C-f>:call cursor(0,11)<CR>
nnoremap <leader>ss :vimgrep // ./**/*<C-f>:call cursor(0,11)<CR>
nnoremap <leader><leader>sa :vimgrep /\<\>/ ./**/*.cpp ./**/*.cc ./**/*.c ./**/*.h ./**/*.hpp<C-f>:call cursor(0,13)<CR>
nnoremap <leader><leader>sc :vimgrep /\<\>/ ./**/*.cpp ./**/*.cc ./**/*.c<C-f>:call cursor(0,13)<CR>
nnoremap <leader><leader>sh :vimgrep /\<\>/ ./**/*.h ./**/*.hpp<C-f>:call cursor(0,13)<CR>
nnoremap <leader><leader>s% :vimgrep /\<\>/ %<C-f>:call cursor(0,13)<CR>
nnoremap <leader><leader>ss :vimgrep /\<\>/ ./**/*<C-f>:call cursor(0,13)<CR>

" g@: call the function set by the 'operatorfunc'
" <SID>: use for function namespace
" <c-u>: clear the command line to the begin
" visualmode(): vim inside function to get the last visual mode type: v, V, <C-v>
" the two map below are for nomal mode, visual mode
" how to use: <localleader>giw, viw<localleader>g ...
nnoremap <leader>g :set operatorfunc=<SID>GrepOperatorR<CR>g@
vnoremap <leader>g :<c-u>call <SID>GrepOperator(visualmode(), 1)<CR>
nnoremap <leader>G :set operatorfunc=<SID>GrepOperatorNR<CR>g@
vnoremap <leader>G :<c-u>call <SID>GrepOperator(visualmode(), 0)<CR>
" better use than <leader>G
nnoremap <leader><leader>g :set operatorfunc=<SID>GrepOperatorNR<CR>g@
vnoremap <leader><leader>g :<c-u>call <SID>GrepOperator(visualmode(), 0)<CR>

function! s:GrepOperatorR(type)
    call s:GrepOperator(a:type, 1)
endfunction

function! s:GrepOperatorNR(type)
    call s:GrepOperator(a:type, 0)
endfunction

" s: use namespace s
function! s:GrepOperator(type, recursion)
    " save the unnamed register before use
    let saved_unnamed_register = @@

    " visual mode: characterwise
    " copy the visual selected text to unnamed register
    " ==#: case-sensitive
    if a:type ==# 'v'
        normal! `<v`>y
    " normal mode: characterwise motion
    " copy the motion text(like iw/i[) to unnamed register
    elseif a:type ==# 'char'
        normal! `[v`]y
    " others right return for the reson grep can not deal with
    else
        return
    endif

    " execute the grep for searching
    " !: do not go to the first result, just fill the quickfix list
    " :copen<CR>: open the quickfix window
    " silent: do not display the message when running command
    " shellescape: to deal whit kind like words <that's> which contain single quote in grep
    if(&l:filetype ==# 'cpp' || &l:filetype ==# 'c')
        if a:recursion
            silent execute "grep! -R " . shellescape(@@) . " --include=*.{c,cc,cpp,h,hpp} ."
        else
            silent execute "grep! " . shellescape(@@) . " %"
        endif
    else
        if a:recursion
            silent execute "grep! -R " . shellescape(@@) . " ."
        else
            silent execute "grep! " . shellescape(@@) . " %"
        endif
    endif
    " open the quickfix list window
    silent execute "copen"
    silent execute "normal! \<C-w>J"
    silent execute "normal! \<C-l>"
    let g:quickfix_l_is_open = 1

    " restore the unnamed register after use
    let @@ = saved_unnamed_register
endfunction

" }}}

" ==========convenient map for file finding========== {{{
nnoremap <leader>f :find ./**/
nnoremap <leader><leader>f :find ./**/<C-r><C-w>
" }}}

" ==========convenient map for translating========== {{{
nnoremap <leader>t :Translate <C-r><C-w>
nnoremap <leader><leader>t :Translate! 
vnoremap <leader>t :Translate
vnoremap <leader><leader>t :Translate!
" nnoremap <leader>t :TranslateW <C-r><C-w>
" nnoremap <leader><leader>t :TranslateW! 
" vnoremap <leader>t :TranslateW
" vnoremap <leader><leader>t :TranslateW!
" }}}

" ==========cursor shape and color setting========== {{{
" Set cursor shape and color
" before . setting shape, after . setting color
" 1 -> blinking block  é—ªçƒçš„æ–¹å—
" 2 -> solid block  ä¸é—ªçƒçš„æ–¹å—
" 3 -> blinking underscore  é—ªçƒçš„ä¸‹åˆ’çº¿
" 4 -> solid underscore  ä¸é—ªçƒçš„ä¸‹åˆ’çº¿
" 5 -> blinking vertical bar  é—ªçƒçš„ç«–çº¿
" 6 -> solid vertical bar  ä¸é—ªçƒçš„ç«–çº¿
if &term =~ "xterm"
    let &t_SI = "\<Esc>[1 q"    "start INSERT mode
    let &t_SR = "\<Esc>[1 q"    "start REPLACE mode
    let &t_EI = "\<Esc>[2 q"    "end insert or replace mode
    let &t_VS = "\<Esc>[2 q"    "NORMAL mode
    " let &t_SI = "\<Esc>[1 q" . "\<Esc>]12;rgb:CD/B3/8B\x7"
    " let &t_SR = "\<Esc>[1 q" . "\<Esc>]12;rgb:CD/B3/8B\x7"
    " let &t_EI = "\<Esc>[2 q" . "\<Esc>]12;rgb:CD/B3/8B\x7"
    " let &t_VS = "\<Esc>[2 q" . "\<Esc>]12;rgb:CD/B3/8B\x7"
endif
" }}}

" ==========toggle setting========== {{{
" toggle nrformats, æ•°å€¼è¿›åˆ¶æ•æ„Ÿæ€§è®¾ç½®
" set nrformats=
nnoremap <F8> :call <SID>NumberFormatsToggle()<CR>
function! s:NumberFormatsToggle()
    if &nrformats == ''
        let &nrformats = g:nrformats_origin
        echo "toggle number formats = " . g:nrformats_origin
    else
        let g:nrformats_origin = &nrformats
        let &nrformats = ''
        echo "toggle number formats = null"
    endif
endfunction

" toggle number
nnoremap <leader>N :setlocal number!<CR>

" toggle open/close quickfix window
" TODO bug: when use command 'copen' g:quickfix_l_is_open cannot be updated
" learn what is wincmd wã€winnr()
nnoremap <leader>w :call <SID>QuickfixToggle()<CR>
function! s:QuickfixToggle()
    if g:quickfix_l_is_open
        cclose
        let g:quickfix_l_is_open = 0
    else
        copen
        silent execute "normal! \<C-w>J"
        let g:quickfix_l_is_open = 1
    endif
endfunction
" }}}

" ==========section movements========== {{{
" If your '{' or '}' are not in the first column, and you would like to use "[["
" and "]]" anyway, try these mappings: bug!
" map [[ ?{<CR>w99[{
" map ][ /}<CR>b99]}
" map ]] j0[[%/{<CR>
" map [] k$][%?}<CR>
" bug fix in my way
nmap [[ []%
nmap ]] %][%
" the same as below
" nmap <silent> ]] :<c-u>execute "normal! %"<CR>][:<c-u>silent execute "normal! %"<CR>
"
" è§£é‡Š: æ®µè·¯è·³è½¬[[ã€]]ã€[]ã€][ï¼Œä»¥c++ä¸ºä¾‹è¿›è¡Œè®²è§£ï¼Œç¬¦å·{ä¸ºå‡½æ•°çš„å¼€å§‹,ç¬¦å·}ä¸ºå‡½æ•°çš„ç»“æŸ
" [[: è·³è½¬åˆ°ä¸Šä¸€ä¸ªå‡½æ•°çš„å¼€å§‹
" ]]: è·³è½¬åˆ°ä¸‹ä¸€ä¸ªæˆ–å½“å‰å‡½æ•°çš„å¼€å§‹
" []: è·³è½¬åˆ°ä¸Šä¸€ä¸ªå‡½æ•°çš„ç»“æŸ
" ][: è·³è½¬åˆ°ä¸‹ä¸€ä¸ªå‡½æ•°çš„ç»“æŸ
" å…¶ä¸­]]çš„è¡Œä¸ºæ˜¯æœ€ç‰¹æ®Šçš„ï¼Œå‡è®¾å½“å‰å…‰æ ‡åœ¨å‡½æ•°ä½“å†…éƒ¨ï¼Œ]]åè€Œä¼šä½¿å…‰æ ‡å‘ä¸Šç§»åŠ¨åˆ°å½“å‰å‡½æ•°çš„å¼€å§‹{,
" è¿™ä¸"ä¸‹ä¸€ä¸ª"çš„è¡Œä¸ºä¼¼ä¹æœ‰äº›çŸ›ç›¾ï¼Œä¸€æ–¹é¢æ˜¯ç”±äºæˆ‘ä»¬è‡ªå·±çš„å®ç°é€ æˆçš„ï¼Œä½†æ˜¯åªè¦æŠŠå®ƒç†è§£ä¸º
" â€œè·³è½¬åˆ°ä¸‹ä¸€ä¸ªæˆ–å½“å‰å‡½æ•°çš„å¼€å§‹â€œå°±å¯ä»¥äº†ï¼Œå¹¶ä¸”]]ä¹Ÿæ˜¯æœ€å¸¸ä½¿ç”¨çš„ï¼Œ"è·³è½¬åˆ°ä¸‹ä¸€ä¸ªæˆ–å½“å‰å‡½æ•°çš„å¼€å§‹"
" è¿™ä¸€è¡Œä¸ºçœŸçš„å¾ˆæ£’ï¼Œè¦æƒ³è·³è½¬åˆ°ç»“æŸå¯ä»¥ä½¿ç”¨%

" }}}

"==========window setting========== {{{
" set window size to 0 but not close
nnoremap <leader>x :<c-u>vertical resize 0<CR>
nnoremap <leader><leader>x :<c-u>resize 0<CR>
" max window size
nnoremap <leader>o :<c-u>resize 9999<CR>:<c-u>vertical resize 9999<CR>:echo "max window size"<CR>
" add/reduce window size
nnoremap <S-Up> :<c-u>resize -1<CR>
nnoremap <S-Down> :<c-u>resize +1<CR>
nnoremap <S-Left> :<c-u>vertical resize -1<CR>
nnoremap <S-Right> :<c-u>vertical resize +1<CR>
" }}}

" ==========binary position========== {{{
" This is the most powerful cursor moving action created by wonderful!!!
" As its name shows the cursor moving acts in binary mode
" <C-j> <C-k> binary move the cursor in vertical direction
" <C-h> <C-k> binary move the cursor in horizontal direction
" The cursor will always be in middle when first moving
" If you miss the target call <leader>c to clear the position and try again
" The binary position is especially useful when edit with which is not English,
" beacuse it will be difficult to use f to find where you want to go
nnoremap <C-j> :<c-u>call <SID>BinaryPositionV("down", "null")<CR>
nnoremap <C-k> :<c-u>call <SID>BinaryPositionV("up", "null")<CR>
nnoremap <C-h> :<c-u>call <SID>BinaryPositionH("left", "null")<CR>
nnoremap <C-l> :<c-u>call <SID>BinaryPositionH("right", "null")<CR>

nnoremap v vmv
vnoremap <C-j> :<c-u>call <SID>BinaryPositionV("down", "visual")<CR>
vnoremap <C-k> :<c-u>call <SID>BinaryPositionV("up", "visual")<CR>
vnoremap <C-h> :<c-u>call <SID>BinaryPositionH("left", "visual")<CR>
vnoremap <C-l> :<c-u>call <SID>BinaryPositionH("right", "visual")<CR>

noremap <leader>c :<c-u>call <SID>BinaryClear()<CR>

function! s:ResetV()
    let g:v_beg = line('w0')
    let g:v_end = line('w$')
    let g:v_mid = g:v_beg + (g:v_end - g:v_beg) / 2
    let g:v_last_p = 0
endfunction

function! s:ResetH()
    let g:h_beg = 1
    let g:h_end = col('$')
    let g:h_mid = g:h_beg + (g:h_end - g:h_beg) / 2
    let g:h_last_p = 0
endfunction

function! s:BinaryClear()
    call <SID>ResetV()
    call <SID>ResetH()
    echom "Binary position has been cleared!"
endfunction

function! s:VisualMark()
    silent execute "normal! mw`vv`w"
endfunction

function! s:BinaryPositionV(direction, type)
    let v_p = line('w0')
    if v_p != g:v_last_p
       call <SID>ResetV()
       call cursor(g:v_mid, 0)
       let g:v_last_p = v_p
       if a:type ==? 'visual'
           call <SID>VisualMark()
       endif
       echom "call BinaryPositionV(\"" . a:direction . "\")" . " -> beg: " .  g:v_beg . " end: " . g:v_end . " mid: " . g:v_mid
       return
   endif

   if a:direction ==? 'down'
       let g:v_beg = g:v_mid
   elseif a:direction ==? 'up'
       let g:v_end = g:v_mid
   endif

   let g:v_mid = g:v_beg + (g:v_end - g:v_beg) / 2

   call cursor(g:v_mid, 0)

   if a:type ==? 'visual'
       call <SID>VisualMark()
   endif

   echom "call BinaryPositionV(\"" . a:direction . "\")" . " -> beg: " .  g:v_beg . " end: " . g:v_end . " mid: " . g:v_mid
endfunction

function! s:BinaryPositionH(direction, type)
    let h_p = line('.')
    if h_p != g:h_last_p
        call <SID>ResetH()
        call cursor(0, g:h_mid)
        let g:h_last_p = h_p
        if a:type ==? 'visual'
           call <SID>VisualMark()
        endif
        echom "call BinaryPositionH(\"" . a:direction . "\")" . " -> beg: " .  g:h_beg . " end: " . g:h_end . " mid: " . g:h_mid
        return
    endif

    if a:direction ==? 'left'
        let g:h_end = g:h_mid
    elseif a:direction ==? 'right'
        let g:h_beg = g:h_mid
    endif

    let g:h_mid = g:h_beg + (g:h_end - g:h_beg) / 2

    call cursor(0, g:h_mid)

    if a:type ==? 'visual'
       call <SID>VisualMark()
    endif

    echom "call BinaryPositionH(\"" . a:direction . "\")" . " -> beg: " .  g:h_beg . " end: " . g:h_end . " mid: " . g:h_mid
endfunction
" }}}

" ==========tool functions========== {{{
" remove left zero: 050 -> 50
function! RemoveLeftZero(number)
    " echom 'input: ' . a:number
    let length = strlen(a:number)
    if length == 0
        return '0'
    endif
    let i = 0
    for n in split(a:number, '\zs')
        if n != '0'
            break
        endif
        let i += 1
    endfor
    if i == length
        return a:number
    else
        return a:number[i:length-1]
    endif
endfunction

" time to millisecond, input format: 23:03:55.998 or 03:55.998
function! TimeToMillisecond(time)
    " echom 'input: ' . a:time
    let length = len(a:time)
    if length == 0
        return 0
    endif
    let time_list = split(a:time, '\.')
    let front_time_list = split(time_list[0], ":")
    if len(front_time_list) == 2
        return RemoveLeftZero(front_time_list[0]) * 60 * 1000 + RemoveLeftZero(front_time_list[1]) * 1000 + RemoveLeftZero(time_list[1])
    elseif len(front_time_list) == 3
        return RemoveLeftZero(front_time_list[0]) * 60 * 60 * 1000 + RemoveLeftZero(front_time_list[1]) * 60 * 1000 + RemoveLeftZero(front_time_list[2]) * 1000 + RemoveLeftZero(time_list[1])
    else
        echo 'error time format!'
        return 0
    endif
endfunction
" }}}

" ==========register macro record========== {{{
" éå¸¸æœ‰ç”¨çš„å®ä¿å­˜åˆ°å¯„å­˜å™¨ä¸­ï¼Œåœ¨å¼€å‘æ—¶æ–¹ä¾¿ä½¿ç”¨ï¼Œå°±åƒå·¥å…·ç®±ä¸€æ ·
" [ç»å…¸å¯„å­˜å™¨å†…å®¹è¿½åŠ ]ï¼Œç”¨äºç»Ÿè®¡æŸ¥æ‰¾æ¨¡å¼åçš„æ•°æ®ï¼Œæ¯”å¦‚æœ‰å¦‚ä¸‹logæ—¥å¿—ï¼š
" xxx
" time diff: 123
" xxx
" time diff: -23
" xxx
" time diff: 37
" ...
" é¦–å…ˆæ‰§è¡ŒæŸ¥æ‰¾æ¨¡å¼ï¼š '/time diff: '
" ç„¶åä¸²è¡Œæ‰§è¡Œå®ï¼š100@u
" æ•°æ®å°†ä¿å­˜åˆ°å¯„å­˜å™¨zä¸­: '123 -23 37 '
let @u = 'gnl"ZyW'
" [ç›¸é‚»æ•°æ®å·®å€¼è®¡ç®—]ï¼Œä¸@uç±»ä¼¼
" é¦–å…ˆæ‰§è¡ŒæŸ¥æ‰¾æ¨¡å¼ï¼š '/time diff: '
" ç„¶åä¸²è¡Œæ‰§è¡Œå®ï¼š100@v
" æ•°æ®å°†ä¿å­˜åˆ°å¯„å­˜å™¨zä¸­: '-146 60 '
" let @v = 'gnl"jyiWgnl"kyiW:let t = @k - @j:let @z = @z . t . " "Nh'
let @v = 'gnl"jyEgnl"kyE:let @j = RemoveLeftZero(@j):let @k = RemoveLeftZero(@k):let t = @k - @j:let @z = @z . t . " "Nh'
" [ç›¸é‚»æ—¶é—´å·®å€¼è®¡ç®—]ï¼Œæ—¶é—´æ ¼å¼åº”è¯¥å½¢å¦‚: xxxæ—¶:åˆ†:ç§’.æ¯«ç§’ ä¾‹: I:time [17:12:39.638 11-03-2022]
" é¦–å…ˆæ‰§è¡ŒæŸ¥æ‰¾æ¨¡å¼ï¼š 'I:time ['
" ç„¶åä¸²è¡Œæ‰§è¡Œå®ï¼š100@w
" ä¸€æ¯«ç§’ä¸ºå•ä½çš„æ—¶é—´å·®æ•°æ®å°†ä¿å­˜åˆ°å¯„å­˜å™¨zä¸­
" let @w = 'gnl"tyiw:let i = @t * 60 * 60 * 1000f:w"tyiw:let i += @t * 60 * 1000;w"tyiw:let i += @t * 1000f.w"tyiw:let i += @tgnl"tyiw:let j = @t * 60 * 60 * 1000f:w"tyiw:let j += @t * 60 * 1000;w"tyiw:let j += @t * 1000f.w"tyiw:let j += @t:let t = j - i:let @z = @z . t . " "Nh'
let @w = 'gnl"jyEgnl"kyE:let @j = TimeToMillisecond(@j):let @k = TimeToMillisecond(@k):let t = @k - @j:let @z = @z . t . " "Nh'
" [ä»¥ç©ºæ ¼ä½œä¸ºé—´éš”ç¬¦æ‹¼æ¥æ‰€æœ‰è¡Œ]
" å³æŠŠæ‰€æœ‰è¡Œä»¥ç©ºæ ¼ä¸ºé—´éš”æ‹¼æ¥åˆ°ä¸€è¡Œï¼Œæˆ–è€…å°†æ‰€æœ‰è¡Œçš„æ¢è¡Œç¬¦æ›¿æ¢ä¸ºç©ºæ ¼
" ç›´æ¥ä¸²è¡Œæ‰§è¡Œå®ï¼š100@x
let @x = '"Zy$:let @z = @z . " "j'
" [æ‹·è´æ•´è¡Œæ•°æ®] åœ¨æŸ¥çœ‹æ—¥å¿—æ—¶ä½¿ç”¨grepï¼Œæœ‰æ—¶ä¼šæ˜¾ç¤ºå¾ˆé•¿çš„æ–‡ä»¶åå‰ç¼€,
" ä½¿ç”¨æ•´è¡Œæ•°æ®æ‹·è´å¯ä»¥é¿å…è¿™ç§éº»çƒ¦
" å…ˆæ‰§è¡ŒæŸ¥æ‰¾æ¨¡å¼ï¼š'/[message]:'
" æ‰§è¡Œå®ä¸€æ¬¡: @y
" æ•°æ®å°†ä¿å­˜åˆ°å¯„å­˜å™¨zä¸­ï¼Œåœ¨ä¸€ä¸ªç¼“å†²åŒºç²˜è´´å‡ºæ¥':put z'
let @y = ':global//yank Z'
" [é•¿è¡ŒååŠéƒ¨åˆ†æˆªå–] æ˜¯ä¸Šé¢@yçš„åŠ å¼ºç‰ˆ, åœ¨ä¸€è¡Œå¾ˆé•¿çš„æ–‡æœ¬ä¸­æˆªå–ååŠéƒ¨åˆ†æœ‰ç”¨çš„å†…å®¹,
" è¿™åœ¨æ—¥å¿—åˆ†æä¸­å½“å‰ç¼€å†…å®¹å¤ªé•¿æ—¶éå¸¸æœ‰ç”¨
" å…ˆæ‰§è¡ŒæŸ¥æ‰¾æ¨¡å¼ï¼š'/message: '
" ç„¶åä¸²è¡Œæ‰§è¡Œå®ï¼š100@y
" æ•°æ®å°†ä¿å­˜åˆ°å¯„å­˜å™¨zä¸­ï¼Œåœ¨ä¸€ä¸ªç¼“å†²åŒºç²˜è´´å‡ºæ¥':put z'
let @o = 'gnmmo"ty$:let @t = @t . "\n":let @z = @z . @t`ml'
" æç¤ºï¼šè¿è¡Œå®ä¹‹å‰åº”è¯¥å…ˆæ¸…ç©ºå¯„å­˜å™¨z -> qzq
" }}}

" ==========hex show========== {{{
nnoremap <silent> <F6> :call <SID>HexShowToggle()<CR>
function! s:HexShowToggle()
    let g:hex_show = !g:hex_show
    if g:hex_show
        silent execute "%!xxd"
        echo "hex show"
    else
        silent execute "%!xxd -r"
        echo "restore from hex"
    endif
endfunction
" }}}
